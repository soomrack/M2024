&ACCESS RVP
&REL 3
DEF  tower ( )
DECL FRAME HOME_1
DECL FRAME START_TOWER
DECL FRAME START_KUBE
DECL FRAME MOVE_UP
DECL FRAME MOVE_DOWN
DECL INT KUBE
DECL INT COUNTER

;FOLD INI
  ;FOLD BASISTECH INI
    GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
    INTERRUPT ON 3 
    BAS (#INITMOV,0 )
  ;ENDFOLD (BASISTECH INI)
  ;FOLD USER INI
    ;Make your modifications here

  ;ENDFOLD (USER INI)
;ENDFOLD (INI)

;FOLD PTP HOME  Vel= 100 % DEFAULT;%{PE}%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:, 5:100, 7:DEFAULT
$BWDSTART = FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS (#PTP_PARAMS,100 )
$H_POS=XHOME
PTP  XHOME
;ENDFOLD

   OPEN_GRAB()
	KUBE = 30
	$BASE = $WORLD

	HOME_1 = {X 94.9, Y 862.6, Z 43.9, A 146, B 0, C 0}
	START_TOWER = {X 0, Y 300, Z 0, A 0, B 0, C 0}
	START_KUBE = {X 0, Y 0, Z 0, A 0, B 0, C 0}
		
	$BASE = HOME_1	
	
	FOR COUNTER = 0 TO 5 STEP 1
	   START_KUBE.X = START_KUBE.X - KUBE - 8
	   
	   MOVE_DOWN = START_KUBE
	   MOVE_DOWN.Z = MOVE_DOWN.Z + 50
	   
	   PTP MOVE_DOWN    
		PTP START_KUBE
		CLOSE_GRAB() 
		PTP MOVE_DOWN
		               
		MOVE_DOWN = START_TOWER
		MOVE_DOWN.Z = MOVE_DOWN.Z + 50
	   
	   PTP MOVE_DOWN 
		PTP START_TOWER
		OPEN_GRAB()
		PTP MOVE_DOWN
		START_TOWER.Z = START_TOWER.Z + KUBE
	ENDFOR
	
	FOR COUNTER = 0 TO 5 STEP 1
	   START_TOWER.Z = START_TOWER.Z - KUBE
	   MOVE_DOWN = START_TOWER
      MOVE_DOWN.Z = MOVE_DOWN.Z + 150
      
		PTP MOVE_DOWN
	   PTP START_TOWER
	   CLOSE_GRAB()
		PTP MOVE_DOWN
		 
		MOVE_DOWN = START_KUBE
      MOVE_DOWN.Z = MOVE_DOWN.Z + 150

	   
	   PTP MOVE_DOWN    
		PTP START_KUBE
		OPEN_GRAB()
		PTP MOVE_DOWN  
		START_KUBE.X = START_KUBE.X + KUBE + 8
	ENDFOR


;FOLD PTP HOME  Vel= 100 % DEFAULT;%{PE}%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:, 5:100, 7:DEFAULT
$BWDSTART = FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS (#PTP_PARAMS,100 )
$H_POS=XHOME
PTP  XHOME
;ENDFOLD

END

DEF CLOSE_GRAB ()
	$OUT[3]=TRUE
	WAIT SEC 1.5
	$OUT[3]=FALSE
END

DEF OPEN_GRAB ()
	$OUT[4]=TRUE
	WAIT SEC 1.5
	$OUT[4]=FALSE
END